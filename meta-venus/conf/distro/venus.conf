DISTRO = "venus"
DISTRO_NAME = "Victron Energy"
DISTRO_VERSION = "v3.70~5_NodeJS-22.16"
SDK_VENDOR = "-ve"
SDK_VERSION := "${DISTRO_VERSION}"

MAINTAINER = "Jeroen Hofstee <jhofstee@victronenergy.com>"

TARGET_VENDOR = "-ve"

LOCALCONF_VERSION = "1"
LAYER_CONF_VERSION ?= "6"

INHERIT:remove = "create-spdx"

DISTRO_FEATURES = " \
    bluetooth \
    bluez5 \
    fbdev \
    gles2 \
    ipv4 \
    ipv6 \
    opengl \
    sysvinit \
    usbhost \
    usbgadget \
    wayland \
    wifi \
    ${DISTRO_FEATURES_LIBC} \
"

# See backfilling, this gets enabled by default unless unset
DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio"

USERADDEXTENSION = "useradd-staticids"
USERADD_ERROR_DYNAMIC = "error"

BAD_RECOMMENDATIONS = "rng-tools"

ROOTFS_RO_UNNEEDED = "${ROOTFS_BOOTSTRAP_INSTALL}"
IMAGE_NAME_SUFFIX = ""

COREVERSION = "${@d.getVar('LAYERSERIES_CORENAMES').split()[0]}"
SDK_NAME = "${DISTRO}-${COREVERSION}-${SDK_ARCH}-${TARGET_ARCH}-${DEFAULTTUNE}"
SDKPATHINSTALL := "/opt/${DISTRO}/${COREVERSION}-${TARGET_ARCH}-${DEFAULTTUNE}"

OLDEST_KERNEL = "5.10"
QEMU_TARGETS ?= "arm x86_64"
PREFERRED_PROVIDER_make = "make"
PREFERRED_PROVIDER_make-native = "make-native"
PREFERRED_VERSION_swupdate = "2016.10"
PREFERRED_PROVIDER_u-boot-fw-utils = "u-boot-fw-utils"

PREFERRED_VERSION_python3-pymodbus = "2.5.3"

# The gui to start, a machine can override this.
START_GUI ?= "start-gui-v1"

# make sure qt6 and qt4 utils don't clash
QT6_INSTALL_BINDIR = "${bindir}/qt6"
QT6_INSTALL_INCLUDEDIR = "${includedir}/qt6"
QT6_INSTALL_LIBEXECDIR = "${libexecdir}/qt6"
QT6_INSTALL_DATADIR = "${datadir}/qt6"
QT6_INSTALL_DOCDIR = "${docdir}/qt6"
QT6_INSTALL_TRANSLATIONSDIR = "${datadir}/translations/qt6"

SWU_VERSION ?= "1"
SWU ?= "venus-swu${@d.getVar('SWU_VERSION') > '1' and '-' + d.getVar('SWU_VERSION') or ''}"

VOLATILE_LOG_DIR = "no"

# Recommended settings per https://wiki.mozilla.org/Security/Server_Side_TLS
OPENSSL_CIPHERSTRING = " \
    ECDHE-ECDSA-AES128-GCM-SHA256 \
    ECDHE-RSA-AES128-GCM-SHA256 \
    ECDHE-ECDSA-AES256-GCM-SHA384 \
    ECDHE-RSA-AES256-GCM-SHA384 \
    ECDHE-ECDSA-CHACHA20-POLY1305 \
    ECDHE-RSA-CHACHA20-POLY1305 \
    DHE-RSA-AES128-GCM-SHA256 \
    DHE-RSA-AES256-GCM-SHA384 \
    DHE-RSA-CHACHA20-POLY1305 \
"
OPENSSL_CIPHERSTRING_COLONS := "${@':'.join(d.getVar('OPENSSL_CIPHERSTRING').split())}"

OPENSSL_CIPHERSUITES = " \
    TLS_AES_128_GCM_SHA256 \
    TLS_AES_256_GCM_SHA384 \
    TLS_CHACHA20_POLY1305_SHA256 \
"
OPENSSL_CIPHERSUITES_COLONS := "${@':'.join(d.getVar('OPENSSL_CIPHERSUITES').split())}"

# Move opkg data out of /var/lib since it will become and overlayfs with a readonly rootfs.
# See also opkg_%.bbappend.
OPKGLIBDIR = "${libdir}"

#PREMIRRORS ?=
#MIRRORS =+ ""

INHERIT += "venus-checks"

# The CONNECTIVITY_CHECK_URI's are used to test whether we can succesfully
# fetch from the network (and warn you if not). To disable the test set
# the variable to be empty.
# Git example url: git://git.yoctoproject.org/yocto-firewall-test;protocol=git;rev=HEAD

CONNECTIVITY_CHECK_URIS ?= ""

SANITY_TESTED_DISTROS ?= " \
    ubuntu-20.04 \n \
    ubuntu-22.04 \n \
    ubuntu-24.04 \n \
"

# Default hash policy for distro
BB_SIGNATURE_HANDLER ?= 'OEBasicHash'
